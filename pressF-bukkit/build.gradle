import dev.s7a.gradle.minecraft.server.MinecraftServerConfig
import dev.s7a.gradle.minecraft.server.tasks.LaunchMinecraftServerTask

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'dev.s7a.gradle.minecraft.server' version '1.1.0'
}

tasks.withType(JavaCompile) {
    // override, compile targeting J17
    options.release = 17
}

repositories {
    maven { url = uri("https://papermc.io/repo/repository/maven-public/") }
    maven { url = uri("https://oss.sonatype.org/content/groups/public/") }
}

dependencies {
    implementation project(':pressF-common')
    compileOnly 'io.papermc.paper:paper-api:1.19-R0.1-SNAPSHOT'
    // https://github.com/Carleslc/Simple-YAML
    implementation 'me.carleslc.Simple-YAML:Simple-Yaml:1.8'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand (
                'pluginVersion': project.pluginVersion,
                'pluginDescription': project.pluginDescription
        )
        include 'plugin.yml'
    }
}

minecraftServerConfig {
    jarUrl.set("https://api.papermc.io/v2/projects/paper/versions/1.19/builds/43/downloads/paper-1.19-43.jar")
}

task buildAndLaunchServer(type: LaunchMinecraftServerTask) {
    dependsOn tasks.jar
    doFirst {
        copy {
            from(buildDir.toPath().resolve("libs/pressF-${project.pluginVersion}-bukkit.jar".toString()))
            into(buildDir.toPath().resolve("MinecraftServer/plugins"))
        }
    }
    jarUrl.set("https://api.papermc.io/v2/projects/paper/versions/1.19/builds/43/downloads/paper-1.19-43.jar")
}

shadowJar {
    archiveName = "pressF-${project.pluginVersion}-bukkit.jar"

    exclude 'module-info.class'
    exclude 'META-INF/maven/**'
    exclude 'META-INF/proguard/**'
}

artifacts {
    archives shadowJar
    shadow shadowJar
}